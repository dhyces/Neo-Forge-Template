plugins {
    id "eclipse"
    id "idea"
    id "maven-publish"
    alias(libs.plugins.neogradle)
    alias(libs.plugins.mdg)
}

modsDotGroovy {
    dslVersion = libs.versions.mdg.dsl.get()
    platform = "forge"
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

sourceSets {
    main
    datagen {
        compileClasspath += main.compileClasspath + main.output
        runtimeClasspath += main.runtimeClasspath + main.output
    }
}

java.toolchain.getLanguageVersion().set(JavaLanguageVersion.of(17))

runs {
    configureEach {
        workingDirectory project.file("run")

        systemProperty "forge.logging.markers", "REGISTRIES"
        systemProperty "forge.logging.console.level", "debug"

        modSource sourceSets.main
    }

    client {
        systemProperty "forge.enabledGameTestNamespaces", mod_id
    }

    server {
        systemProperty "forge.enabledGameTestNamespaces", mod_id
        programArguments.addAll "--nogui"
    }

    gameTestServer {
        systemProperty "forge.enabledGameTestNamespaces", mod_id
    }

    data {
        programArguments.addAll "--mod", mod_id, "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

        modSource sourceSets.datagen
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }

dependencies {
    implementation(libs.neoforge)
}

tasks.named("processResources", ProcessResources).configure {
    exclude(".cache", "mods.groovy")
}

java {
    withSourcesJar()
    withJavadocJar()
}

sourcesJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_id}" }
    }
}

tasks.named("jar", Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy "reobfJar"
}

publishing {
    publications {
        register("mavenJava", MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.getRelease().set(17)
}
